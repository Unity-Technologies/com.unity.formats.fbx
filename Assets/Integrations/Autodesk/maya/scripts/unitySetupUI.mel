global string $unityMenuName = "UnityFbxForMaya";
global string $unityMenuDivider = "UnityFbxForMayaDivider";
global string $unityMenuLabel = "Unity";
global string $unityMenuInsertAfter = "exportActiveFileOptions";

global string $unityImportIconPath = "import.png";
global string $unityExportIconPath = "export.png";
global string $unityIconPath = "unity.png";

global string $unityPluginVersion = "{Version}";
global string $unityFamilyLabel = "The UnityFbxForMaya plugin allows you to reliably exchange and review your work between Maya and Unity.";
global string $unityImportLabel = "Import an FBX file from a Unity project and auto-configure for exporting";
global string $unityExportLabel = "Export Model to Unity";

global string $unityCommandsFile = "unityCommands.mel";
global string $unityImportMenuName = "Import";
global string $unityExportMenuName = "Export";
global string $unityImportCommand = "unityImport";
global string $unityExportCommand = "unityExport";
global string $unityRemoveSendToUnityMenuOption = "UnityFbxForMaya_removeSendToUnityMenu";
global string $unitySendToUnityMenuName = "sendToUnityMenu";

global proc string unityWhatsNewVersion(){
    return `about -q -version`;

}

global proc unitySetupUI(){

    $isHeadless = `optionVar -q "UnityFbxForMaya_Headless"`;
    if($isHeadless){
        // headless install, don't add UI
        return;
    }    

    evalDeferred -lowestPriority "unityInstallUI";
}

global proc string unitySetLabelVersion(string $label){
    global string $unityPluginVersion;
    return `format -stringArg $label -stringArg $unityPluginVersion "^1s (v.^2s)"`;
}

global proc unityInstallUI(){
    global string $unityMenuName;
    global string $unityMenuDivider;
    global string $unityMenuLabel;
    global string $unityMenuInsertAfter;

    global string $unityImportIconPath;
    global string $unityExportIconPath;
    global string $unityIconPath;

    global string $unityPluginVersion;
    global string $unityFamilyLabel;
    global string $unityImportLabel;
    global string $unityExportLabel;
    global string $unityCommandsFile;
    global string $unityImportMenuName;
    global string $unityExportMenuName;
    global string $unityImportCommand;
    global string $unityExportCommand;
    
    global string $unityRemoveSendToUnityMenuOption;
    global string $unitySendToUnityMenuName;
    
    global string $gMainFileMenu;
    
    // update family label with version
    $unityFamilyLabel = unitySetLabelVersion($unityFamilyLabel);
    
    buildFileMenu;
    $parentMenu = $gMainFileMenu;
    $pluginVersion = unityWhatsNewVersion();
    eval ("source " + $unityCommandsFile);
    menuItem -parent $parentMenu -insertAfter $unityMenuInsertAfter -divider true -longDivider false -version $pluginVersion $unityMenuDivider;
    menuItem -parent $parentMenu -insertAfter $unityMenuDivider -subMenu true -label $unityMenuLabel -tearOff true -version $pluginVersion -image $unityIconPath -annotation $unityFamilyLabel $unityMenuName;
    menuItem -parent $unityMenuName -label $unityImportMenuName -version $pluginVersion -image $unityImportIconPath -annotation $unityImportLabel -command $unityImportCommand;
    menuItem -parent $unityMenuName -label $unityExportMenuName -version $pluginVersion -image $unityExportIconPath -annotation $unityExportLabel -command $unityExportCommand;
    
    $unityRemoveSendToUnityMenuOption = `optionVar -q $unityRemoveSendToUnityMenuOption`;
    if($unityRemoveSendToUnityMenuOption && `menu -exists $unitySendToUnityMenuName`){
        menu -e -visible false $unitySendToUnityMenuName;
    }    

}